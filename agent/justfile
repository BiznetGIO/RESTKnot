#!/usr/bin/env -S just --justfile

alias f := fmt
alias l := lint

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup:
    poetry install && poetry shell

# Build the docker image.
build-image:
    docker build -t restknot-agent:latest --build-arg VCS_REVISION=$(git rev-parse --short HEAD) .

# Run the docker image.
run-image: build-image
    docker-compose up

# Format the codebase.
fmt:
    poetry run black .
    poetry run isort .

# Check is the codebase properly formatted.
fmt-check:
    poetry run black --check .

# Lint the codebase.
lint:
    poetry run ruff .

# Tasks to make the code-base comply with the rules. Mostly used in git hooks.
comply: fmt lint

# Check if the repository comply with the rules and ready to be pushed.
check: fmt-check lint

# Check dependencies health.
up:
    poetry lock
    poetry show --outdated | grep --file=<(poetry show --tree | grep '^\w' | cut -d' ' -f1) || echo "All dependencies are up to date, yay!"
