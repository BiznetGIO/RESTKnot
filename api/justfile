#!/usr/bin/env -S just --justfile

shebang := if os() == 'windows' { 'powershell.exe' } else { '/usr/bin/sh' }

set dotenv-load := true

alias d := dev
alias r := run
alias f := fmt
alias l := lint
alias t := test

# List available commands.
_default:
    just --list --unsorted

# Setup the repository
setup:
    sqlx --version || cargo install sqlx-cli --no-default-features --features postgres,native-tls
    cargo watch --version || cargo install cargo-watch
    cargo outdated --version || cargo install --locked cargo-outdated

# Develop the app.
dev:
    cargo watch -x 'clippy --locked --all-targets --all-features'

# Develop the app.
run:
    cargo run

# Format the codebase.
fmt:
    cargo fmt --all

# Check is the codebase is properly formatted.
fmt-check:
    cargo fmt --all -- --check

# Lint the codebase.
lint:
    cargo clippy --locked --all-targets --all-features

# Check the documentation.
_doc-check:
    cargo doc --all-features --no-deps

# Run the unit tests.
_unit-test:
    cargo test --lib

# Test the codebase.
test:
    cargo test

_update-sqlx-schema:
    cargo sqlx prepare -- --lib

_check-sqlx-schema:
    cargo sqlx prepare --check -- --lib

# reset the database schema.
_reset-db:
    sqlx database drop && sqlx database create

# Tasks to make the code base comply with the rules. Mostly used in git hooks.
comply: fmt lint test _doc-check _update-sqlx-schema

# Check if the repository complies with the rules and is ready to be pushed.
check: _check-sqlx-schema fmt-check lint test _doc-check

# Check dependencies' health. Pass `--write` to upgrade dependencies.
up arg="":
    #!{{ shebang }}
    if [ "{{ arg }}" = "--write" ]; then
    	cargo upgrade
    	cargo update
    else
        cargo outdated --root-deps-only
    fi;
