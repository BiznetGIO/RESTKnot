#!/usr/bin/env -S just --justfile

set dotenv-load := true

alias r := run
alias f := fmt
alias l := lint
alias t := test

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup: _areyousure
    # psycopg2 needs these
    # sudo apt-get install --no-install-recommends --yes build-essential libpq-dev
    # sudo dnf install libpq-devel

    rye sync

# Tasks to make the code-base comply with the rules. Mostly used in git hooks.
comply: fmt lint test-unit

# Check if the repository comply with the rules and ready to be pushed.
check: fmt-check lint test

[doc('Run the app')]
run:
    FLASK_APP=rkapi.autoapp rye run flask run --port 8001

# Build the docker image.
image-build:
    rye build --wheel --clean
    docker build -t restknot-api:latest --build-arg VCS_REVISION=$(git rev-parse --short HEAD) .

# Run the container image.
image-start:
    docker compose up -d

# Stop the container image.
image-stop:
    docker compose down

# Format the codebase.
fmt:
    rye fmt

# Check is the codebase properly formatted.
fmt-check:
    rye fmt --check

# Lint the codebase.
lint:
    rye lint

# Test the codebase.
test: test-unit
    rye test -- tests/integration/

test-unit:
    rye test -- tests/unit/

# Check dependencies health.
up:
    rye sync --update-all

#
# Helper
#

[unix]
_areyousure:
    #!/usr/bin/env bash
    echo -e "This command will alter your system. ⚠️
    You are advised to run in inside containerized environment.
    Such as [toolbx](https://containertoolbx.org/).

    If you are unsure. Run the installation commands manually.
    Take a look at the 'setup' recipe in the Justfile.\n"

    read -p "Are you sure you want to proceed? (Y/n) " response;
    if [[ $response =~ ^[Yy] ]]; then
        echo "Continue!";
    else
        echo "Cancelled!";
        exit 1;
    fi
