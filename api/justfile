#!/usr/bin/env -S just --justfile

set dotenv-load := true

alias r := run
alias f := fmt
alias l := lint
alias t := test

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup:
    poetry install && poetry shell

# Build the docker image.
build-image:
    docker build -t restknot-api:latest --build-arg VCS_REVISION=$(git rev-parse --short HEAD) .

# Run the docker image.
run-image: build-image
    docker-compose up

# Develop the app.
run:
    flask run

# Format the codebase.
fmt:
    poetry run black .
    poetry run isort .
    dprint fmt --config ../configs/dprint.json

# Check is the codebase properly formatted.
fmt-check:
    poetry run black --check .
    dprint check --config ../configs/dprint.json

# Lint the codebase.
lint:
    poetry run  ruff .

_test-unit:
    poetry run pytest -s tests/unit/

# Test the codebase.
test: _test-unit
    poetry run pytest -s tests/integration/

# Tasks to make the code-base comply with the rules. Mostly used in git hooks.
comply: fmt lint _test-unit

# Check if the repository comply with the rules and ready to be pushed.
check: fmt-check lint test

# Check dependencies health.
up:
    poetry lock
    poetry show --outdated | grep --file=<(poetry show --tree | grep '^\w' | cut -d' ' -f1) || echo "All dependencies are up to date, yay!"
