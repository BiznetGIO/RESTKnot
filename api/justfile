#!/usr/bin/env -S just --justfile

set dotenv-load := true

alias d := dev
alias f := fmt
alias l := lint
alias t := test

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup:
    poetry shell && poetry install

# Develop the app.
dev:
    flask run

# Format the codebase.
fmt:
    black .
    isort .
    dprint fmt --config ../configs/dprint.json

# Check is the codebase properly formatted.
fmt-check:
    black --check .
    dprint check --config ../configs/dprint.json

# Lint the codebase.
lint:
    ruff .

_test-unit:
    pytest -s tests/unit/

# Test the codebase.
test: _test-unit
    pytest -s tests/integration/

# Tasks to make the code-base comply with the rules. Mostly used in git hooks.
comply: fmt lint _test-unit

# Check if the repository comply with the rules and ready to be pushed.
check: fmt-check lint test

# Check dependencies health.
up:
    poetry show --outdated | grep --file=<(poetry show --tree | grep '^\w' | cut -d' ' -f1)
